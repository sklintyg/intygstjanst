plugins {
    id "se.inera.intyg.plugin.common" version "2.0.2" apply false
    id "org.gretty" version "2.3.1" apply false
}

ext {
    commonVersion = System.properties['commonVersion'] ?: '0-SNAPSHOT'
    infraVersion = System.properties['infraVersion'] ?: '0-SNAPSHOT'
    refDataVersion = System.properties['refDataVersion'] ?: '1.0-SNAPSHOT'

    schemasContractVersion = "2.1.7"

    intygHealthcondRehabilitationSchemasVersion = "1.1.2"
    intygClinicalprocessSchemasVersion = "1.0.16"
    rivtaClinicalprocessSchemasVersion = "4.0"
    rivtaItintegrationMonitoringSchemasVersion = "1.0.0.4"

    activeMqVersion = "5.13.5"
    assertjVersion = "3.6.2"
    geronimoJmsVersion = "1.1.1"
    hikariCPVersion = "2.3.5"
    jadiraUserTypeVersion = "5.0.0.GA"
    jpaHibernateVersion = "1.0.0.Final"
    jsonSchemaValidatorVersion = "2.8.0"
    liquibaseVersion = "3.3.5"
    restassuredVersion = "2.8.0"
    servletApiVersion = "2.5"
    springDataVersion = "1.11.12.RELEASE"
    springVersion = "4.3.16.RELEASE"
    stAntlr4Version = "4.0.8"
    xmlUnitVersion = "1.4"

    mockitoVersion = "2.16.0"
    mysqlVersion = "5.1.46"
    h2Version = "1.4.196"
}

allprojects {
    apply plugin: 'se.inera.intyg.plugin.common'
    apply plugin: 'maven'

    repositories {
        jcenter()
        mavenLocal()
        maven { url "https://build-inera.nordicmedtest.se/nexus/repository/snapshots/" }
        maven { url "https://build-inera.nordicmedtest.se/nexus/repository/releases/" }
        maven { url "http://repo.maven.apache.org/maven2" }
    }

    dependencies {
        testCompile "se.inera.intyg.refdata:refdata:${refDataVersion}"
        testCompile "junit:junit:4.+"
        testCompile "org.mockito:mockito-core:${mockitoVersion}"
    }

    group = 'se.inera.intyg.intygstjanst'
    version = System.properties['buildVersion'] ?: '0-SNAPSHOT'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    task createVersionPropertyFile(type: VersionPropertyFileTask)

    uploadArchives.repositories.mavenDeployer {
        repository(url: "https://build-inera.nordicmedtest.se/nexus/repository/releases/") {
            authentication(userName: System.properties['nexusUsername'], password: System.properties['nexusPassword'])
        }
    }

    compileJava {
        options.encoding = "UTF-8"
    }

    [install.repositories.mavenInstaller, uploadArchives.repositories.mavenDeployer]*.pom*.whenConfigured { pom ->
        pom.dependencies.findAll {dep -> dep.groupId == 'se.inera.intyg.common'}.each {
            it.version = se.inera.intyg.IntygPluginKt.findResolvedVersion(project, 'se.inera.intyg.common')
        }
        pom.dependencies.findAll {dep -> dep.groupId == 'se.inera.intyg.infra'}.each {
            it.version = se.inera.intyg.IntygPluginKt.findResolvedVersion(project, 'se.inera.intyg.infra')
        }
    }

}

task tagRelease(type: TagReleaseTask)

