plugins {
    id "se.inera.intyg.plugin.common" version "3.2.7" apply false
    id "org.gretty" version "3.0.8" apply false
    id "io.spring.dependency-management" version "1.0.13.RELEASE" apply false
}

ext {
    commonVersion = System.properties['commonVersion'] ?: '3.20.0-SNAPSHOT'
    infraVersion = System.properties['infraVersion'] ?: '3.20.0-SNAPSHOT'
    refDataVersion = System.properties['refDataVersion'] ?: '1.0-SNAPSHOT'

    intygHealthcondRehabilitationSchemasVersion = "1.1.3.1"
    intygClinicalprocessSchemasVersion = "1.0.24.1"
    rivtaClinicalprocessSchemasVersion = "4.0.5.2"
    rivtaItintegrationMonitoringSchemasVersion = "1.0.0.5.1"
    schemasContractVersion = "2.1.8.1"

    cxfVersion = "3.5.5"
    geronimoJmsVersion = "1.1.1"
    jadiraUserTypeVersion = "6.0.1.GA"
    jakartaJwsApiVersion = "3.0.0"
    javaxJwsApiVersion = "1.1"
    jaxb2Version = "0.13.1"
    picocliVersion = "4.6.3"
    stAntlr4Version = "4.3.4"
    shedlockSpringVersion = "4.42.0"

    springbootMavenBomVersion = "2.7.6"
}

allprojects {
    apply plugin: 'se.inera.intyg.plugin.common'
    apply plugin: 'maven'
    apply plugin: "io.spring.dependency-management"

    repositories {
        mavenLocal()
        maven { url "https://nexus.drift.inera.se/repository/it-public/" }
        mavenCentral {
            content {
                // this repository contains everything BUT artifacts with group starting with "se.inera"
                excludeGroupByRegex "se\\.inera.*"
            }
        }
    }

    test {
        useJUnitPlatform()
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springbootMavenBomVersion}"
        }
    }

    dependencies {
        implementation "javax.jws:javax.jws-api:${javaxJwsApiVersion}"

        compile "javax.annotation:javax.annotation-api"
        compile "jakarta.xml.bind:jakarta.xml.bind-api"
        compile "jakarta.xml.ws:jakarta.xml.ws-api"
        compile "jakarta.jws:jakarta.jws-api:${jakartaJwsApiVersion}"
        compile "com.sun.xml.messaging.saaj:saaj-impl"
        compileOnly 'org.projectlombok:lombok'

        testCompile "se.inera.intyg.refdata:refdata:${refDataVersion}"
        testCompile "junit:junit"
        testCompile "org.mockito:mockito-core"
        testCompile "org.junit.jupiter:junit-jupiter-api"
        testCompile "org.junit.jupiter:junit-jupiter-params"
        testCompile "org.mockito:mockito-junit-jupiter"
        testCompileOnly 'org.projectlombok:lombok'
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
        testRuntimeOnly "org.junit.vintage:junit-vintage-engine"

        annotationProcessor 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'
    }

    group = 'se.inera.intyg.intygstjanst'
    version = System.properties['buildVersion'] ?: '0-SNAPSHOT'

    sourceCompatibility = 11
    targetCompatibility = 11

    task createVersionPropertyFile(type: VersionPropertyFileTask)

    uploadArchives.repositories.mavenDeployer {
        repository(url: "https://nexus.drift.inera.se/repository/maven-releases/") {
            authentication(userName: System.properties['ineraNexusUsername'], password: System.properties['ineraNexusPassword'])
        }
    }

    compileJava {
        options.encoding = "UTF-8"
    }

    uploadArchives.enabled = false

    [install.repositories.mavenInstaller, uploadArchives.repositories.mavenDeployer]*.pom*.whenConfigured { pom ->
        pom.dependencies.findAll { dep -> dep.groupId == 'se.inera.intyg.common' }.each {
            it.version = se.inera.intyg.IntygPluginKt.findResolvedVersion(project, 'se.inera.intyg.common')
        }
        pom.dependencies.findAll { dep -> dep.groupId == 'se.inera.intyg.infra' }.each {
            it.version = se.inera.intyg.IntygPluginKt.findResolvedVersion(project, 'se.inera.intyg.infra')
        }
    }

}

gradle.taskGraph.whenReady {
    def runningRestAssuredTest = false
    project('intygstjanst-web') { runningRestAssuredTest = gradle.taskGraph.getAllTasks().contains(restAssuredTest) }

    if (runningRestAssuredTest) {
        project('intygstjanst-web') { test.enabled(false) }
        project('intygstjanst-persistence') { test.enabled(false) }
    }
}

task tagRelease(type: TagReleaseTask)

