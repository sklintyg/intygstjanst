apply plugin: 'scala'

ext {
    gatlingVersion = '2.1.7'
    gatlingBaseUrl =  System.properties['certificate.baseUrl'] ?: "http://localhost:8080/inera-certificate"
    simulation = System.properties['gatlingSimulation']
}

repositories {
    mavenCentral()
}

sourceSets.test.output.classesDir = new File(buildDir, "classes/scala")

task gatling(dependsOn: 'build') {
    doLast {
        javaexec {
            main = 'io.gatling.app.Gatling'
            classpath = sourceSets.test.output + sourceSets.test.runtimeClasspath
            jvmArgs "-DbaseUrl=$gatlingBaseUrl"
            args    '-sf', sourceSets.test.output,                          //source folder
            '-bf', sourceSets.test.output.classesDir,               //target folder where gatling will compile the simulation
            '-s' , "se.inera.intygstjanst.$simulation",   //The simulation to run
            '-rf', 'build/reports/gatling'                          //place to put the report
        }
    }
}

dependencies {
    testCompile "io.gatling:gatling-app:${gatlingVersion}"
    testCompile "io.gatling:gatling-recorder:${gatlingVersion}"
    testCompile "io.gatling.highcharts:gatling-charts-highcharts:${gatlingVersion}"
    testCompile "org.scalaj:scalaj-http_2.11:2.2.0"
}
