description = 'Intygstj√§nsten (Web)'

apply plugin: 'war'
apply plugin: 'org.akhikhl.gretty'

test {
    exclude '**/*IT*'
}

task restAssuredTest(type: Test) {
    outputs.upToDateWhen { false }
    systemProperty "integration.tests.baseUrl", System.properties['baseUrl'] ?: "http://localhost:8080/"
    include '**/*IT*'
}

gretty {
    integrationTestTask = null
    contextPath = '/inera-certificate'
    contextConfigFile = file("src/main/webapp/WEB-INF/jetty-web.xml")
    scanInterval = 0

    jvmArgs = [
        "-Dcatalina.base=${buildDir}/catalina.base",
        "-Dspring.profiles.active=dev",
        "-Drecipient.config.file=${projectDir}/recipient-dev.properties",
        "-Dcredentials.file=${projectDir}/certificate-credentials.properties",
        "-Dcertificate.logback.file=${projectDir}/certificate-logback.xml",
        "-Dcertificate.config.file=${projectDir}/certificate-dev.properties"
        ]
}

war.dependsOn createVersionPropertyFile

dependencies {
    compile project(':intygstjanst-persistence')

    compile "se.inera.intyg.common:hsa-integration:${commonVersion}"

    compile "se.inera.intyg.intygstyper:ts-bas:${typerVersion}"
    compile "se.inera.intyg.intygstyper:ts-diabetes:${typerVersion}"
    compile "se.inera.intyg.intygstyper:ts-parent:${typerVersion}"
    compile "se.inera.intyg.intygstyper:fk-parent:${typerVersion}"
    compile "se.inera.intyg.intygstyper:luse:${typerVersion}"
    compile "se.inera.intyg.intygstyper:lisu:${typerVersion}"
    compile "se.inera.intyg.intygstyper:luae_fs:${typerVersion}"
    compile "se.inera.intyg.intygstyper:luae_na:${typerVersion}"
    compile ("se.inera.intyg.intygstyper:fk7263:${typerVersion}") {
        exclude(module: 'insuranceprocess-healthreporting-schemas')
        exclude(module: 'fk-vardgivare-sjukvard-schemas')
        exclude(module: 'ServiceContracts_itintegration_monitoring')
    }

    compile 'commons-io:commons-io:2.4'
    compile "org.springframework:spring-jms:${springVersion}"
    compile 'se.riv.infrastructure.directory.organization:infrastructure-directory-organization-schemas:1.1.RC5.3'
    compile 'se.inera.intyg.clinicalprocess.healthcond.rehabilitation:intyg-clinicalprocess-healthcond-rehabilitation-schemas:1.0.3'
    compile "se.riv.itintegration.monitoring:itintegration-monitoring-schemas:1.0.0.3"
    compile "org.springframework:spring-webmvc:${springVersion}"

    compileOnly 'org.apache.geronimo.specs:geronimo-jms_1.1_spec:1.1.1'
    compileOnly 'javax.servlet:servlet-api:2.5'

    testCompile "org.springframework:spring-test:${springVersion}"
    testCompile 'xmlunit:xmlunit:1.4'
    testCompile 'com.jayway.restassured:rest-assured:2.8.0'
    testCompile 'com.jayway.restassured:json-schema-validator:2.8.0'
    testCompile 'org.antlr:ST4:4.0.8'

    gretty "org.apache.activemq:activemq-client:${activeMqVersion}"
    gretty "org.apache.activemq:activemq-broker:${activeMqVersion}"
    gretty "com.h2database:h2:${h2Version}"
    gretty "mysql:mysql-connector-java:${mysqlVersion}"
}
